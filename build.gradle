plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.21'
}

group 'net.mlcoder'
version '0.0.4'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    codegen
}

sourceSets {
     codegen {
        java {
            srcDir 'src/codegen/java'
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.8'

    codegenCompileOnly 'org.projectlombok:lombok:1.18.20'
    codegenAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    codegenImplementation group: 'org.apache.commons', name: 'commons-text', version: '1.8'
    codegenImplementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    codegenImplementation group: 'org.freemarker', name: 'freemarker', version: '2.3.30'
    codegenImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'
    codegenImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
    codegenImplementation group: 'com.google.guava', name: 'guava', version: '28.2-jre'
    codegenImplementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2020.1.1'
}
patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}

task generateLatexTemplate(type: JavaExec) {
    group = "script"
    classpath = sourceSets.codegen.runtimeClasspath

    workingDir("${project.rootDir}/src/main/resources/templates/latex")
    args "${project.rootDir}/src/codegen/data/latex"

    mainClass = "net.mlcoder.codegen.LatexTemplateGenerator"
}

task generateSymbols(type: JavaExec) {
    group = "script"
    classpath = sourceSets.codegen.runtimeClasspath
    workingDir("${project.rootDir}/src/main/java/net/mlcoder/unimath/category")

    mainClass = "net.mlcoder.codegen.JavaSymbolFileGenerator"
}

tasks.compileJava.dependsOn generateLatexTemplate
tasks.compileJava.dependsOn generateSymbols

